import java.nio.file.Files
import java.nio.file.Paths
import java.util.regex.Pattern

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(":nuvotifier-bukkit")
    compile project(":nuvotifier-bungeecord")
    compile project(":nuvotifier-sponge")
    compile project(":nuvotifier-velocity")
}

shadowJar {
    baseName = 'nuvotifier'
    classifier = null
    relocate "io.netty", "com.vexsoftware.votifier.netty"
    relocate "org.json", "com.vexsoftware.votifier.json"
    relocate "com.google.code", "com.vexsoftware.votifier.google"
    relocate "com.google.guava", "com.vexsoftware.votifier.google"
    relocate "org.apache.commons.io", "com.vexsoftware.votifier.commons.io"
}

artifacts {
    tasks.shadowJar.outputs.files.each {
        archives(it) {
            builtBy shadowJar
        }
    }
}

def artifactPattern = Pattern.compile(/(.*)-([0-9]+\.)*[0-9]+(-SNAPSHOT)?\.jar/)

task('linkLatest') {
    dependsOn tasks.shadowJar
    doLast {
        tasks.shadowJar.outputs.files.each {
            System.println(it.absolutePath)
            def noVerMatcher = artifactPattern.matcher(it.absolutePath)
            if (!noVerMatcher.matches())
                throw new IllegalStateException("wtf")

            def noVer = Paths.get(noVerMatcher.group(1) + ".jar")
            def versnd = it.toPath()

            Files.createSymbolicLink(noVer, versnd)
        }
    }
}

build.dependsOn linkLatest
